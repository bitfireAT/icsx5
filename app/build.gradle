apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'
apply plugin: 'com.google.devtools.ksp'

android {
    compileSdkVersion 33
    buildToolsVersion '33.0.2'

    namespace 'at.bitfire.icsdroid'

    defaultConfig {
        applicationId "at.bitfire.icsdroid"
        minSdkVersion 21
        targetSdkVersion 33

        versionCode 73
        versionName "2.2-beta.1"

        setProperty "archivesBaseName", "icsx5-" + getVersionCode() + "-" + getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig = true
        compose = true
        dataBinding = true
        viewBinding = true
    }

    composeOptions {
        // Keep in sync with Kotlin version: https://developer.android.com/jetpack/androidx/releases/compose-kotlin
        kotlinCompilerExtensionVersion = '1.4.7'
    }

    flavorDimensions "distribution"
    productFlavors {
        standard
        gplay
    }

    signingConfigs {
        bitfire {
            storeFile file(System.getenv("ANDROID_KEYSTORE") ?: "/dev/null")
            storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
            keyAlias System.getenv("ANDROID_KEY_ALIAS")
            keyPassword System.getenv("ANDROID_KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.bitfire
        }
    }

    lint {
        disable 'ExtraTranslation', 'MissingTranslation', 'InvalidPackage', 'OnClick'
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/*.md']
        }
    }

    androidResources {
        generateLocaleConfig true
    }
}

configurations {
    all {
        // exclude modules which are in conflict with system libraries
        exclude module: "commons-logging"
        exclude group: "org.json", module: "json"

        // Groovy requires SDK 26+, and it's not required, so exclude it
        exclude group: 'org.codehaus.groovy'
    }
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'

    implementation 'com.github.bitfireAT:cert4android:3817e62d9f173d8f8b800d24769f42cb205f560e'
    implementation 'com.github.bitfireAT:ical4android:a78e72f580'

    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.fragment:fragment-ktx:1.5.7'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime-ktx:2.8.1'
    implementation 'com.google.android.material:material:1.9.0'

    // Jetpack Compose
    def composeBom = platform("androidx.compose:compose-bom:${versions.composeBom}")
    implementation composeBom
    androidTestImplementation composeBom
    implementation 'androidx.compose.material:material'
    debugImplementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.runtime:runtime-livedata:1.5.0"
    implementation 'com.google.accompanist:accompanist-themeadapter-material:0.30.1'
    implementation 'io.github.vanpra.compose-material-dialogs:color:0.9.0'

    implementation 'com.jaredrummler:colorpicker:1.1.0'
    implementation "com.mikepenz:aboutlibraries-compose:${versions.aboutLibs}"
    implementation "com.squareup.okhttp3:okhttp:${versions.okhttp}"
    implementation "com.squareup.okhttp3:okhttp-brotli:${versions.okhttp}"
    implementation "com.squareup.okhttp3:okhttp-coroutines:${versions.okhttp}"
    implementation "joda-time:joda-time:2.12.5"

    // latest commons that don't require Java 8
    //noinspection GradleDependency
    implementation 'commons-io:commons-io:2.6'
    //noinspection GradleDependency
    implementation 'org.apache.commons:commons-lang3:3.8.1'

    // Room Database
    implementation "androidx.room:room-ktx:${versions.room}"
    ksp "androidx.room:room-compiler:${versions.room}"

    // for tests
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation "androidx.test:rules:1.5.0"
    androidTestImplementation "androidx.arch.core:core-testing:2.2.0"
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:${versions.okhttp}"
    androidTestImplementation "androidx.work:work-testing:2.8.1"

    testImplementation 'junit:junit:4.13.2'
}

aboutLibraries {
    duplicationMode = com.mikepenz.aboutlibraries.plugin.DuplicateMode.MERGE
    includePlatform = false
}